────────────────────────────────────────────────────────
Lab: Special Searches
Author: Roger Hui
To advance the lab, select menu Help|Studio|Advance or the
corresponding shortcut.


── Chapter 1    Introduction ───────────────────────────

── ((1) 1 of 1) Introduction ───────────────────────────

Some common search phrases are "recognized" by the
system and are implemented by special code, resulting in
savings in time and space.  This lab discusses the
following groups of such phrases:

f i. 1:   first place where x f y
+./@:f    does x f y anywhere?
i.<./     where the minimum first occurs
m&i.      fixing the left argument of i.
)

── Chapter 2    f i. 1: ────────────────────────────────

── ((2) 1 of 5) f i. 1: ────────────────────────────────

The phrase  x (> i. 1:) y  finds the first place where x
is greater than y .
)
   ] x=: 10 ?.@$ 288
162 20 100 66 209 28 274 28 116 43
   ] y=: 10 ?.@$ 388
206 336 272 62 145 172 150 232 272 163
   x (>i.1:) y
3
   (x>y) i. 1
3

── ((2) 2 of 5) f i. 1: (ctd) ──────────────────────────

x (>i.1:) y is a fork, equivalent to the following:
)
   x (>i.1:) y
3
   (x > y) i. (x 1: y)  NB. definition of a fork
3
   (x > y) i. 1         NB. constant verb 1:
3

── ((2) 3 of 5) f i. 1: (ctd) ──────────────────────────

Finding the first place where x is greater than y is
done conventionally by the phrase  (x>y)i.1 .
x>y compares x and y for all elements, creating a
Boolean list in so doing, then finds the first 1 in the
list.  The time savings in  x(>i.1:)y  come from
comparing only as many elements as necessary to find the
first 1, while avoiding creating the Boolean list.  The
space savings come from not having to create the list.

If a "hit" comes at the beginning, the search ends
instantaneously.  Even if a hit comes at the end, the
time savings are by a factor of at least 2.  In either
case the space used is constant instead of linear.
)
   ts=: 6!:2 , 7!:2@]  NB. time and space
   
   ratio=: 4 : 0
 t0=. ts 'x (>i.1:) y'  NB. with special code
 t1=. ts '(x>y)i.1'     NB. without special code
 t0,t1,t1%t0
)
   
   NB. time-space with special code, without special code, ratios
   
   x=: 1e5 ?@$ 2e9
   x ratio 0    NB. found at the beginning
1e_6 1152 3.6e_5 132128 36 114.694
   x ratio 2e9  NB. found at the end
6.7e_5 1216 0.000206 132128 3.07463 108.658
   
   x=: 1e6 ?@$ 2e9
   x ratio 0    NB. found at the beginning
1e_6 1152 0.000298 1.04963e6 298 911.139
   x ratio 2e9  NB. found at the end
0.000553 1216 0.001447 1.04963e6 2.61664 863.184

── ((2) 4 of 5) f i. 1: (ctd) ──────────────────────────

Other forks similar to (>i.1:) are likewise recognized
and are supported by special code:

a. various propositions:  = ~: < <: >: > E. e.
b. finding first place or last place:  i. i:
c. finding 1 or 0

That is,
   f i. 1:    first place where     x f y
   f i. 0:    first place where not x f y
   f i: 1:    last  place where     x f y
   f i: 0:    last  place where not x f y

Some restrictions apply:
- Special code is available only for Boolean, integer,
  floating point, literal, and symbol arguments
- Special code is used only for atoms and lists if f is
  not e.
- If f is E. , special code only for   E.i.1:
)

── ((2) 5 of 5) f i. 1: (ctd) ──────────────────────────
Some examples to illustrate the use of these searches:
)
   NB. index of first vowel
   'boustrophedonic' (e.i.1:) 'aeiou'
1
   NB. index of last  vowel
   'boustrophedonic' (e.i:1:) 'aeiou'
13
   NB. index of first consonant
   'boustrophedonic' (e.i.0:) 'aeiou'
0
   NB. index of last  consonant
   'boustrophedonic' (e.i:0:) 'aeiou'
14
   NB. index of the word "ago"
   'ago' (E.i.1:) 'fourscore and ten years ago, our fathers'
24

── Chapter 3    +./@:f ─────────────────────────────────

── ((3) 1 of 1) +./@:f ─────────────────────────────────

Another group of recognized search phrases are:
   + /@:f    number of places where x f y
   +./@:f    x f y anywhere?
   *./@:f    x f y everywhere?
   I. @:f    indices where x f y

+./@:f and *./@:f will do only as many comparisons as
needed to determine the answer.  +/@:f  and  I.@:f  will
necessarily do all the comparisons.  Therefore, the
amount of savings from the special code is data
dependent.  It is possible for there to be little or no
savings.

Alternative phrasings ([: +/ f , and @ instead of @: for
f with infinite ranks) are also recognized.
)
   x=: 1e6 ?@$ 2e9
   y=: 1e6 ?@$ 2e9
   t=: 0 2$0
   t=: t,%/ts&> '+ / x > y'; 'x + /@:> y'
   t=: t,%/ts&> '+./ x > y'; 'x +./@:> y'
   t=: t,%/ts&> '*./ x > y'; 'x *./@:> y'
   t=: t,%/ts&> 'I.  x > y'; 'x  I.@:> y'
   NB. time-space ratios for (no special code) % (special code)
   0j2 ": t
  0.68 713.33
     _ 745.75
813.00 745.75
  1.04   1.00

── Chapter 4    i.<./ ──────────────────────────────────

── ((4) 1 of 1) i.<./ ──────────────────────────────────

The following monads are recognized and implemented by
special code, for integer or floating point lists:
   i.<./   index where the minimum first occurs
   i.>./   index where the maximum first occurs
   i:<./   index where the minimum last  occurs
   i:>./   index where the maximum last  occurs
)
   x=: 1e6 ?@$ 2e9
   ts 'x i. <./x'
0.000633 1408
   ts '(i.<./) x'
0.000632 1408

── Chapter 5    m&i. ───────────────────────────────────

── ((5) 1 of 4) m&i. ───────────────────────────────────

Certain verbs in the "index of" family with a fixed noun
argument m are supported by special code.  That is,
   m&i.        m&(i.!.0)
   m&i:        m&(i:!.0)
   -.&m        -.!.0&m
   e.&m        e.!.0&m
   (e.i.0:)&m  + /@e.&m
   (e.i.1:)&m  +./@e.&m
   (e.i:0:)&m  *./@e.&m
   (e.i:1:)&m   I.@e.&m
are supported by special code, so that for most m and y
the phrase  m&i. y  takes time proportional to the size
of y . ("Most" means amenable to hashing or some other
fast pre-processing technique.) There is no improvement
in performance with just one application; there can be a
substantial improvement in performance with repeated
applications.
)

── ((5) 2 of 4) m&i. (ctd) ─────────────────────────────

Some examples to illustrate the point about "one
application" versus "repeated applications".  In the
following benchmarks:

appl1     1 search
appl1s    1 search   with special code
appl3     3 searches
appl3s    3 searches with special code
)
   m =: 1e6 ?@$ 1e6
   y0=: 1e5 ?@$ 1e6
   y1=: 1e5 ?@$ 1e6
   y2=: 1e5 ?@$ 1e6
   
   appl1 =: 3 : 'm i. y0'
   appl1s=: 3 : ('f=. m&i.'; 'f y0')
   appl3 =: 3 : ('m i. y0'; 'm i. y1'; 'm i. y2')
   appl3s=: 3 : ('f=. m&i.'; 'f y0'; 'f y1'; 'f y2')
   
   ts 'appl1  0'
0.013628 7.3407e6
   ts 'appl1s 0'
0.00205 5.24448e6
   ts 'appl3  0'
0.024681 5.24397e6
   ts 'appl3s 0'
0.001583 5.24448e6

── ((5) 3 of 4) m&i. (ctd) ─────────────────────────────

The difference in time is particularly striking if the
repeated applications are on small arguments.  Thus:
)
   m=: 1e6 ?@$ 1e6
   y=: 10 ?@$ 1e6
   f=: m&i.
   ts 'm i. y'
0.003098 1152
   ts 'f y'
0 1152

── ((5) 4 of 4) m&i. (ctd) ─────────────────────────────

Creating the verb  m&i.  now takes more time and space,
comparable to what is required for one execution of i.~m
However, these costs are amortized over repeated
applications.
)
   m=: 1e6 ?@$ 2e9
   ts 'f=: m&i.'
0.011527 1.67786e7
   ts 'i.~m'
0.008381 2.30697e7

End of lab
   
