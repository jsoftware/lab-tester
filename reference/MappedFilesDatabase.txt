─────────────────────────────────────────────────────────────
Lab: Mapped Files Database
To advance the lab, select menu Help|Studio|Advance or the
corresponding shortcut.


── (1 of 42) Mapped File Database ───────────────────────────
This lab builds a Vehicle Registration (VR) relational
database with mapped names and files.

Lab "Mapped Files" is a prerequisite.

The VR database will have 1 million records.

Each record has 8 fields for a record size of 32 bytes.

The total size of the VR database is 32MB.
)

── (2 of 42) Mapped File Database (ctd) ─────────────────────
A VR record has 8 fields:
  field     type  bytes  value
  lic        int   4     encoding of license plate
  make       char  1     index into MAKES
  color      char  1     index into COLORS
  year       int   4     year of vehicle manufacture
  fine       int   4     total unpaid fines
  firstname  char  6     first name
  lastname   char  8     last name
  comments   int   4     index into comments file
)

── (3 of 42) Mapped File Database (ctd) ─────────────────────
Data for each field is in its own file. The field is used by
mapping a name to the file.

The files for the fields will be in the temp/vr directory.
)

── (4 of 42) Mapped File Database (ctd) ─────────────────────
The next step opens and loads the script that implements the
VR Database.

Take a quick look at the script.
)

── (5 of 42) Mapped File Database (ctd) ─────────────────────
)
   open '~addons/labs/labs/system/mappeddb.ijs'
   load '~addons/labs/labs/system/mappeddb.ijs'

── (6 of 42) Mapped File Database (ctd) ─────────────────────
Clear out and create the temp/vr directory.
)
   vrdeleteall''   NB. delete any from a previous lab run
   createdir PATH  NB. create directory to hold VR files
1
   dir PATH        NB. no files in the directory

── (7 of 42) Mapped File Database (ctd) ─────────────────────
Some of the following steps may take many seconds to run.
Access to the VR database is fast, but creating millions of
initial values takes time.
)

── (8 of 42) Mapped File Database (ctd) ─────────────────────
The next step builds a file for the lic field:
 creates file temp/vr/lic.jmf
 maps name lic to the file
 sets values for lic for each row in the table

Creating 1 million random license encodings takes time.
)

── (9 of 42) Mapped File Database (ctd) ─────────────────────
The first time through the lab do not worry about the details
of the  various build... verbs.
)
   buildlic''  NB. build and map lic file
   dir PATH
lic.jmf     8000728 27-Mar-24 12:33:23
   $lic
1000000
   5{.lic
1516194 1775317 1198374 1732642 1342369

── (10 of 42) Mapped File Database (ctd) ────────────────────
Every field has a format verb of the form field...
)
   sample=: ?.5$ROWS  NB. 5 random rows
   sample{lic         NB. numeric encoding of license plates
1977653 1177930 1709273 1356481 1049991
   licfmt sample      NB. formatted lic values
┌───────┐
│2ECCBA4│
│22CCE41│
│2C1E3C4│
│24AB5D2│
│215EEE2│
└───────┘

── (11 of 42) Mapped File Database (ctd) ────────────────────
)
   buildmake''
   dir PATH
lic.jmf      8000728 27-Mar-24 12:33:23
make.jmf     1000588 27-Mar-24 12:33:24
   [t=: a.i.sample{make     NB. chars - range 0 to #MAKES
9 3 5 3 9
   MAKES
┌────┬─────┬─────┬───────┬──────┬───────┬──────┬─────┬──────┬──┬────────┐
│FORD│DODGE│BUICK│PONTIAC│HUDSON│RAMBLER│TOYOTA│HONDA│ACCURA│VW│MERCEDES│
└────┴─────┴─────┴───────┴──────┴───────┴──────┴─────┴──────┴──┴────────┘
   t{MAKES
┌──┬───────┬───────┬───────┬──┐
│VW│PONTIAC│RAMBLER│PONTIAC│VW│
└──┴───────┴───────┴───────┴──┘

── (12 of 42) Mapped File Database (ctd) ────────────────────
)
   makefmt sample  NB. each field has a format verb
┌───────┐
│VW     │
│PONTIAC│
│RAMBLER│
│PONTIAC│
│VW     │
└───────┘

── (13 of 42) Mapped File Database (ctd) ────────────────────
)
   buildcolor''
   colorfmt sample
┌──────┐
│MAUVE │
│PINK  │
│BLUE  │
│MAROON│
│RED   │
└──────┘

── (14 of 42) Mapped File Database (ctd) ────────────────────
)
   buildyear''  NB. year vehicle was manufactured
   sample{year
1953 1922 1937 1982 1906
   yearfmt sample
┌────┐
│1953│
│1922│
│1937│
│1982│
│1906│
└────┘

── (15 of 42) Mapped File Database (ctd) ────────────────────
)
   buildfine''   NB. total outstanding fines
   finefmt sample
┌─┐
│0│
│0│
│0│
│0│
│0│
└─┘

── (16 of 42) Mapped File Database (ctd) ────────────────────
Let us try some inquiries.
)
   b=: (make=FORD) and (year>1997) and fine>3500
   b#i.ROWS
310900 506700 824900 846800 946100
   [ndx=: records (make=FORD) and (year>1997) and fine>3500
310900 506700 824900 846800 946100

── (17 of 42) Mapped File Database (ctd) ────────────────────
)
   licfmt ndx
┌───────┐
│2EDAEBE│
│2211CD4│
│21CE1E5│
│214E34B│
│2E1C4CE│
└───────┘

── (18 of 42) Mapped File Database (ctd) ────────────────────
)
   (licfmt ndx),makefmt ndx
┌───────┬────┐
│2EDAEBE│FORD│
│2211CD4│FORD│
│21CE1E5│FORD│
│214E34B│FORD│
│2E1C4CE│FORD│
└───────┴────┘

── (19 of 42) Mapped File Database (ctd) ────────────────────
)
   licfmt`makefmt`colorfmt `:0 ndx  NB. evoke gerund append
┌───────┬────┬─────┐
│2EDAEBE│FORD│GREY │
│2211CD4│FORD│MAUVE│
│21CE1E5│FORD│GREY │
│214E34B│FORD│GREY │
│2E1C4CE│FORD│BLUE │
└───────┴────┴─────┘

── (20 of 42) Mapped File Database (ctd) ────────────────────
)
   rep1=: licfmt`makefmt`colorfmt`yearfmt`finefmt `: 0
   rep1 ndx
┌───────┬────┬─────┬────┬────┐
│2EDAEBE│FORD│GREY │1998│4674│
│2211CD4│FORD│MAUVE│1998│4969│
│21CE1E5│FORD│GREY │1998│3506│
│214E34B│FORD│GREY │1998│4218│
│2E1C4CE│FORD│BLUE │1998│4325│
└───────┴────┴─────┴────┴────┘

── (21 of 42) Mapped File Database (ctd) ────────────────────
)
   ('license';'make';'color';'year';'fines') ,: rep1 ndx
┌───────┬────┬─────┬────┬─────┐
│license│make│color│year│fines│
├───────┼────┼─────┼────┼─────┤
│2EDAEBE│FORD│GREY │1998│4674 │
│2211CD4│FORD│MAUVE│1998│4969 │
│21CE1E5│FORD│GREY │1998│3506 │
│214E34B│FORD│GREY │1998│4218 │
│2E1C4CE│FORD│BLUE │1998│4325 │
└───────┴────┴─────┴────┴─────┘

── (22 of 42) Mapped File Database (ctd) ────────────────────
)
   top2=: ('license';'make';'color';'year';'fines')"_
   rep2=: licfmt`makefmt`colorfmt`yearfmt`finefmt `: 0
   report2=: top2 ,: rep2  NB. train
   report2 ndx
┌───────┬────┬─────┬────┬─────┐
│license│make│color│year│fines│
├───────┼────┼─────┼────┼─────┤
│2EDAEBE│FORD│GREY │1998│4674 │
│2211CD4│FORD│MAUVE│1998│4969 │
│21CE1E5│FORD│GREY │1998│3506 │
│214E34B│FORD│GREY │1998│4218 │
│2E1C4CE│FORD│BLUE │1998│4325 │
└───────┴────┴─────┴────┴─────┘

── (23 of 42) Mapped File Database (ctd) ────────────────────
)
   ndx=: records (make e. FORD,TOYOTA) and (color e. PINK,YELLOW) and (year>1995) and fine>3750
   rep2 ndx
┌───────┬┬┬─┬─┐
│       │││ │ │
└───────┴┴┴─┴─┘

── (24 of 42) Mapped File Database (ctd) ────────────────────
)
   report3=: ('license';'fines')"_ ,: licfmt`finefmt `:0
   report3 ndx
┌───────┬─────┐
│license│fines│
├───────┼─────┤
└───────┴─────┘

── (25 of 42) Mapped File Database (ctd) ────────────────────
Let us add a few more fields.
)

── (26 of 42) Mapped File Database (ctd) ────────────────────
)
   buildfirstname''
   firstnamefmt sample  NB. random names
┌──────┐
│Pam   │
│Shawn │
│Milane│
│Munroe│
│Jody  │
└──────┘

── (27 of 42) Mapped File Database (ctd) ────────────────────
)
   buildlastname''
   lastnamefmt sample
┌────────┐
│Aqndrews│
│Rogerson│
│Boudreau│
│Kerik   │
│Sullivan│
└────────┘

── (28 of 42) Mapped File Database (ctd) ────────────────────
)
   [r=: records (make e.FORD,TOYOTA)and(color=PINK)and(year>1995)and fine>2000
522100

── (29 of 42) Mapped File Database (ctd) ────────────────────
)
   top4=: ('firstname';'lastname';'license';'make';'color';'year';'fines')"_
   rep4=: firstnamefmt`lastnamefmt`licfmt`makefmt`colorfmt`yearfmt`finefmt `:0
   report4=: top4 ,: rep4

── (30 of 42) Mapped File Database (ctd) ────────────────────
)
   report4 r
┌─────────┬────────┬───────┬──────┬─────┬────┬─────┐
│firstname│lastname│license│make  │color│year│fines│
├─────────┼────────┼───────┼──────┼─────┼────┼─────┤
│Clare    │Boudreau│22BBC53│TOYOTA│PINK │1996│3134 │
└─────────┴────────┴───────┴──────┴─────┴────┴─────┘

── (31 of 42) Mapped File Database (ctd) ────────────────────
The comment field does not fit simply into the relational
model because it can vary so much in size. Most records have
no comment, some have a small comment, and a few have large
ones. It does not make sense to reserve a rectangular space
that is ROWS times the maxiumum size of a variable length
field.

The comment field will be an index into a jfile of comments.
)

── (32 of 42) Mapped File Database (ctd) ────────────────────
Build comment field file and a jfile with a few comments.
)
   buildcomment''

── (33 of 42) Mapped File Database (ctd) ────────────────────
)
   top5=: ('license';'make';'color';'fines';'comment')"_
   rep5=: licfmt`makefmt`colorfmt`finefmt`commentfmt `:0
   report5=: top5 ,: rep5
   report5 r
┌───────┬──────┬─────┬─────┬─────────────┐
│license│make  │color│fines│comment      │
├───────┼──────┼─────┼─────┼─────────────┤
│22BBC53│TOYOTA│PINK │3134 │long remark 5│
└───────┴──────┴─────┴─────┴─────────────┘

── (34 of 42) Mapped File Database (ctd) ────────────────────
)
   report5 lic i. liccode '2131AD5'
┌───────┬───────┬─────┬─────┬───────┐
│license│make   │color│fines│comment│
├───────┼───────┼─────┼─────┼───────┤
│2131AD5│RAMBLER│PINK │0    │       │
└───────┴───────┴─────┴─────┴───────┘
   report3 lic i. liccode '2123AC5'
┌───────┬─────┐
│license│fines│
├───────┼─────┤
│2123AC5│0    │
└───────┴─────┘

── (35 of 42) Mapped File Database (ctd) ────────────────────
)
   vrunmap''   NB. unmap all VR mapped names
   lic         NB. fields are now undefined
|value error: lic

|[-2] 

── (36 of 42) Mapped File Database (ctd) ────────────────────
)
   vrmap''   NB. map all fields (very quick)

── (37 of 42) Mapped File Database (ctd) ────────────────────
)
   rec=: lic i. liccode '2123AC5'
   report3 rec
┌───────┬─────┐
│license│fines│
├───────┼─────┤
│2123AC5│0    │
└───────┴─────┘

── (38 of 42) Mapped File Database (ctd) ────────────────────
)
   report4 rec
┌─────────┬────────┬───────┬──────┬─────┬────┬─────┐
│firstname│lastname│license│make  │color│year│fines│
├─────────┼────────┼───────┼──────┼─────┼────┼─────┤
│Tom      │Sullivan│2123AC5│HUDSON│PINK │1948│0    │
└─────────┴────────┴───────┴──────┴─────┴────┴─────┘
   lastname=: (8{.'Doe') rec} lastname   NB. new lastname
   firstname=: (6{.'Al') rec} firstname  NB. new firstname
   report4 rec
┌─────────┬────────┬───────┬──────┬─────┬────┬─────┐
│firstname│lastname│license│make  │color│year│fines│
├─────────┼────────┼───────┼──────┼─────┼────┼─────┤
│Al       │Doe     │2123AC5│HUDSON│PINK │1948│0    │
└─────────┴────────┴───────┴──────┴─────┴────┴─────┘

── (39 of 42) Mapped File Database (ctd) ────────────────────
)
   NB.        lic       make    color year fine first last
   addrecord '743ABCD';'DODGE';'BLUE';1998;800; 'Al'; 'Doe'
1000001
   rep4 _1
┌──────┬────────┬───────┬─────┬────┬────┬───┐
│Al    │Doe     │143ABCD│DODGE│BLUE│1998│800│
└──────┴────────┴───────┴─────┴────┴────┴───┘

── (40 of 42) Mapped File Database (ctd) ────────────────────
Report on all registrations with lastname of Doe.
)
   report4 records lastname-:"1 (8{.'Doe')
┌─────────┬────────┬───────┬──────┬─────┬────┬─────┐
│firstname│lastname│license│make  │color│year│fines│
├─────────┼────────┼───────┼──────┼─────┼────┼─────┤
│Al       │Doe     │2123AC5│HUDSON│PINK │1948│  0  │
│Al       │Doe     │143ABCD│DODGE │BLUE │1998│800  │
└─────────┴────────┴───────┴──────┴─────┴────┴─────┘

── (41 of 42) Mapped File Database (ctd) ────────────────────
The field files were created to hold (ROWS+GROWTH) records.
With a GROWTH of 20 you can add 20 records before getting an
allocation error.

When you get an allocation error you need to increase the
size of the file. This cannot be done while the file is
mapped. You need to do a vrunmap; increase the size of all
field files with SetFilePointer and SetEndOfFile; then do
vrmap to remap.
)

── (42 of 42) Mapped File Database (ctd) ────────────────────
This lab has created files in your temp/vr directory. Use
vrdeleteall to delete those files if you are finished with
them.

You can play with the VR database in future sessions. Just
run the mappeddb script and run vrmap to map the fields.

The VR script is: ~addons/labs/labs/system/mappeddb.ijs
)
   NB. to delete the vr directory and all its files, do:
   NB. vrdeleteall ''

End of lab
   
